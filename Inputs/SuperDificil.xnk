imprimir nl "=== ARCHIVO DIFÍCIL ===";


procedimiento separador(cadena t = "-----") {
    imprimir nl t;
}

//  VECTORES 1D 
entero[] v = [100, 26, 1, 15, 167, 0, 76, 94, 25, 44, 5, 59, 95, 10, 23];
entero nV con valor 15;

procedimiento imprimirVector(entero[] arr, entero n) {
    para (entero i con valor 0; i < n; i = i + 1) {
        imprimir nl "v[" + (cadena) i + "] = " + (cadena) arr[i];
    }
}

procedimiento burbuja(entero[] arr, entero n) {
    para (entero i con valor 0; i < n; i = i + 1) {
        para (entero j con valor 0; j < n - i - 1; j = j + 1) {
            si (arr[j] > arr[j + 1]) {
                entero tmp con valor arr[j];
                arr[j] = arr[j + 1];
                arr[j + 1] = tmp;
            }
        }
    }
}

procedimiento demoVector() {
    ejecutar separador("=== VECTOR (ANTES) ===");
    ejecutar imprimirVector(v, nV);
    ejecutar burbuja(v, nV);
    ejecutar separador("=== VECTOR (ORDENADO) ===");
    ejecutar imprimirVector(v, nV);
}

//  MATRIZ 2D (10x10) 
caracter[][] mat = [
    ['.', '.', '.', '.', '.', '.', '.', '.', '.', '.'],
    ['.', '.', '.', '.', '.', '.', '.', '.', '.', '.'],
    ['.', '.', '.', '.', '.', '.', '.', '.', 'D', '.'],
    ['.', '.', '.', '.', '.', '.', '.', 'N', '.', '.'],
    ['.', '.', '.', '.', '.', '.', 'U', '.', '.', '.'],
    ['.', '.', '.', '.', '.', 'M', '.', '.', '.', '.'],
    ['.', '.', '.', '.', '.', '.', '.', '.', '.', '.'],
    ['.', '.', '.', 'A', '.', '.', '.', '.', '.', '.'],
    ['.', '.', 'L', '.', '.', '.', '.', '.', '.', '.'],
    ['.', 'O', '.', '.', '.', '.', '.', '.', '.', '.']
];
entero filas con valor 10;
entero columnas con valor 10;

procedimiento imprimirMatriz(caracter[][] arr, entero f, entero c) {
    para (entero i con valor 0; i < f; i = i + 1) {
        cadena linea con valor "";
        para (entero j con valor 0; j < c; j = j + 1) {
            linea = linea + arr[i][j];
        }
        imprimir nl linea;
    }
}

procedimiento voltearFilas(caracter[][] arr, entero f, entero c) {
    // invertir verticalmente
    para (entero x con valor 0; x < f / 2; x = x + 1) {
        entero y con valor f - 1 - x;
        para (entero j con valor 0; j < c; j = j + 1) {
            caracter t con valor arr[x][j];
            arr[x][j] = arr[y][j];
            arr[y][j] = t;
        }
    }
}

procedimiento demoMatriz() {
    ejecutar separador("=== MATRIZ (ORIGINAL) ===");
    ejecutar imprimirMatriz(mat, filas, columnas);
    ejecutar voltearFilas(mat, filas, columnas);
    ejecutar separador("=== MATRIZ (VOLTEADA) ===");
    ejecutar imprimirMatriz(mat, filas, columnas);
}

// OBJETOS 
objeto Persona {
    cadena nombre;
    entero edad;

    procedimiento setNombre(cadena n) { nombre = n; }
    procedimiento setEdad(entero e) { edad = e; }

    funcion cadena saludar() {
        retornar "Hola, soy " + nombre + " y tengo " + (cadena) edad;
    }
}

procedimiento demoObjetos() {
    Persona p;
    ejecutar p.setNombre("Ana");
    ejecutar p.setEdad(30);
    imprimir nl ejecutar p.saludar();
    // acceso con '.'
    p.edad = p.edad + 1;
    imprimir nl "Edad al siguiente año: " + (cadena) p.edad;
}

// CASTEOS
procedimiento demoCasteos() {
    // Lista permitida (ejemplos):
    // Entero <-> Decimal (parcial)
    // Entero -> Cadena
    // Entero -> Caracter
    // Decimal -> Cadena
    // Caracter -> Entero
    // Caracter -> Decimal

    entero e con valor 65;
    decimal dd con valor 12.75;
    caracter ch con valor 'B';

    // Entero -> Cadena
    cadena se con valor (cadena) e;
    imprimir nl "Entero a cadena: " + se;

    // Entero -> Caracter (65 -> 'A')
    caracter ce con valor (caracter) 65;
    imprimir nl "Entero a caracter: " + ce;

    // Caracter -> Entero ('B' -> 66 aprox)
    entero ei con valor (entero) ch;
    imprimir nl "Caracter a entero: " + (cadena) ei;

    // Caracter -> Decimal
    decimal ed con valor (decimal) ch;
    imprimir nl "Caracter a decimal: " + (cadena) ed;

    // Entero <-> Decimal
    decimal de con valor (decimal) e;  // 65.0
    entero ie con valor (entero) dd;   // 12
    imprimir nl "Entero->Decimal: " + (cadena) de + " | Decimal->Entero: " + (cadena) ie;

    // Decimal -> Cadena
    cadena sd con valor (cadena) dd;
    imprimir nl "Decimal a cadena: " + sd;
}

//  RECURSIVAS
funcion entero fib(entero n) {
    si (n <= 1) { retornar n; }
    retornar ejecutar fib(n - 1) + ejecutar fib(n - 2);
}

procedimiento hanoi(entero n, cadena desde, cadena hacia, cadena aux) {
    si (n == 0) { retornar; }
    ejecutar hanoi(n - 1, desde, aux, hacia);
    imprimir nl "Mover disco " + (cadena) n + " de " + desde + " a " + hacia;
    ejecutar hanoi(n - 1, aux, hacia, desde);
}

funcion entero ack(entero m, entero n) {
    si (m == 0) { retornar n + 1; }
    si (n == 0) { retornar ejecutar ack(m - 1, 1); }
    retornar ejecutar ack(m - 1, ejecutar ack(m, n - 1));
}

procedimiento demoRecursivas() {
    ejecutar separador("=== FIBONACCI ===");
    entero f5 con valor ejecutar fib(5);
    imprimir nl "fib(5) = " + (cadena) f5;

    ejecutar separador("=== HANOI (3 discos) ===");
    ejecutar hanoi(3, "A", "C", "B");

    ejecutar separador("=== ACKERMANN (2,1) ===");
    entero a21 con valor ejecutar ack(2, 1);
    imprimir nl "ack(2,1) = " + (cadena) a21;
}

//  MAIN 
procedimiento main() {
    ejecutar demoVector();
    ejecutar demoMatriz();
    ejecutar demoObjetos();
    ejecutar demoCasteos();
    ejecutar demoRecursivas();
    ejecutar separador("=== FIN ARCHIVO DIFÍCIL ===");
}

ejecutar main();
